TESTMDCB SUBENTRY BASES=(R13,R12)
* PRE-19C WAS 6067 BYTES
* GENERAL ERROR TESTS
* NO BLOCK, POSITIONALS, BAD AM, BAD MF
*        MODCB FRED,MIKE,AM=VTAM,MF=Q
*************
* RPL TESTS *
*************
* BAD PARMS, UNSUPPORTED PARM, BAD REG FOR RPL=, BAD REG FOR MF=L
* BAD KEYLEN/MSGLEN, BAD REG FOR MF=L
*        MODCB RPL=(R15),DDNAME=ABC,MAREA=ABC,OPTCD=(KEY,NUP,FKS),     X
               TRANSID=ABC,ECB=(R14),NXTRPL=(R1),KEYLEN=1234,          X
               MSGLEN=70000,                                           X
               MF=(L,(R14))
* BAD REG FOR ACB/AREA, NO LABEL, NO 2ND PARM FOR MF=G
*        MODCB RPL=MYRPL,ACB=(R0),AREA=(R1),MF=G
* BAD REG FOR MF=E
*        MODCB RPL=MYRPL,MF=(E,(R1))
* OPTCD CONFLICTS FOR ALL MF=
*        MODCB RPL=MYRPL,OPTCD=(KEY,ADR,NUP,UPD,FKS,GEN),MF=I
*MYMODCB  MODCB RPL=MYRPL,OPTCD=(KEY,ADR,NUP,UPD,FKS,GEN),MF=L
*        MODCB RPL=MYRPL,OPTCD=(KEY,ADR,NUP,UPD,FKS,GEN),MF=(E,MYCBMR)
*        MODCB RPL=MYRPL,OPTCD=(KEY,ADR,NUP,UPD,FKS,GEN),MF=(G,MYCBMR)
*
         LA    R14,MYRPL
         AHIK  R15,R14,RPLLEN
         SNAP  STORAGE=((R14),(R15)),PDATA= SNAP 1
* TEST  1: MODIFY THE RPL MYRPL INLINE MF=I
* VERIFIED
         LA    R2,MYRPL
         LA    R5,MYACB
         MODCB RPL=(R2),                                               X
               ACB=(R5),                                               X
               AREA=MYAREA,                                            X
               AREALEN=50,                                             X
               ARG=MYAREA,                                             X
               MSGAREA=MYAREA,                                         X
               MSGLEN=50,                                              X
               KEYLEN=10,                                              X
               OPTCD=(ARD,SKP,XRBA)
         LTR   R15,R15            OK ?
         BNZ   ABORT              EXIT IF NOT
         AHIK  R15,R14,RPLLEN
         SNAP  STORAGE=((R14),(R15)),PDATA= SNAP 2
*
* TEST  2: BUILD CBMR AND MOVE IT TO MYCBMR
* VERIFIED
         MODCB RPL=MYRPL,                                              X
               ACB=MYACB,                                              X
               KEYLEN=50,                                              X
               OPTCD=(ARD,SEQ,XRBA),                                   X
               MF=(L,MYCBMR,MYLEN2)
         LTR   R15,R15            OK ?
         BNZ   ABORT              EXIT IF NOT
         SNAP  STORAGE=(MYCBMR,MYCBMR+MYLEN2),PDATA= SNAP 3
* TEST  3: MODIFY MYCBMR AND USE IT TO MODIFY MYRPL
* VERIFIED
         MODCB RPL=MYRPL,                                              X
               ACB=MYACB2,                                             X
               OPTCD=(LRD,SKP,RBA),                                    X
               MF=(E,MYCBMR)
         LTR   R15,R15            OK ?
         BNZ   ABORT              EXIT IF NOT
         SNAP  STORAGE=(MYCBMR,MYCBMR+MYLEN2),PDATA= SNAP 4
         LA    R14,MYRPL
         AHIK  R15,R14,RPLLEN
         SNAP  STORAGE=((R14),(R15)),PDATA= SNAP 5
* TEST  4: BUILD CBMR AND MOVE IT TO MYCBMR
*          USE CBMR TO MODIFY MYRPL
*          ANY MODS ALREADY MADE TO MYRPL WILL REMAIN
* VERIFIED
         MODCB RPL=(R2),      WAS MYRPL                                X
               AREALEN=1234,                                           X
               NXTRPL=(R5),    WAS MYAREA                              X
               OPTCD=(ADR,SYN),    WAS DIR,ASY                         X
               MF=(G,MYCBMR,MYLEN4)
         LTR   R15,R15            OK ?
         BNZ   ABORT              EXIT IF NOT
         SNAP  STORAGE=(MYCBMR,MYCBMR+MYLEN4),PDATA= SNAP 6
         LA    R14,MYRPL
         AHIK  R15,R14,RPLLEN
         SNAP  STORAGE=((R14),(R15)),PDATA= SNAP 7
* TEST  5: AS TEST 4 BUT WITH REG NOTATION AND CHANGE TO OPTCD
*          USE CBMR TO MODIFY MYRPL
*          ANY MODS ALREADY MADE TO MYRPL WILL REMAIN
* VERIFIED
         LA    R2,MYRPL
         LA    R5,MYAREA
         MODCB RPL=(R2),                                               X
               AREALEN=1234,                                           X
               NXTRPL=(R5),                                            X
               OPTCD=(ADR,SYN),                                        X
               MF=(G,MYCBMR,MYLEN5)
         LTR   R15,R15            OK ?
         BNZ   ABORT              EXIT IF NOT
         SNAP  STORAGE=(MYCBMR,MYCBMR+MYLEN5),PDATA= SNAP 8
         LA    R14,MYRPL
         AHIK  R15,R14,RPLLEN
         SNAP  STORAGE=((R14),(R15)),PDATA= SNAP 9
         AGO   .BYPALL
*************
* ACB TESTS *
*************
* BAD PARMS, UNSUPPORTED PARM, BAD REG FOR ACB=, BAD REG FOR MF=L
* BAD BSTRNO/BUFND/RMODE31/SHRPOOL, BAD REG FOR MF=L
* INVALID/UNSUPPORTED MACRF PARMS
*        MODCB ACB=(R15),ARG=ABC,NXTRPL=ABC,OPTCD=(KEY,NUP,FKS),       X
               MAREA=ABC,MLEN=5,RLSREAD=ABC,PASSWD=(R0),EXLST=(R1),    X
               BSTRNO=300,BUFND=70000,RMODE31=BAD,SHRPOOL=300,         X
               MACRF=(KEY,GOOD,BAD,UGLY,CFX,NCI,RLS),                  X
               MF=(L,(R14))
* BAD REG FOR ACB/PASSWD, NO 2ND PARM FOR MF=G, NO LABEL
*        MODCB ACB=(R0),PASSWD=(R1),MF=G
* BAD REG FOR MF=E
*        MODCB ACB=MYACB,MF=(E,(R1))
* MACRF CONFLICTS FOR ALL MF=
*        MODCB ACB=MYACB,MACRF=(NDF,DFR,NIS,SIS,NRM,AIX,NSR,LSR,GSR),  X
               MF=I
*MYMODCB  MODCB ACB=MYACB,MACRF=(NDF,DFR,NIS,SIS,NRM,AIX,NSR,LSR,GSR), X
               MF=L
*        MODCB ACB=MYACB,MACRF=(NDF,DFR,NIS,SIS,NRM,AIX,NSR,LSR,GSR),  X
               MF=(E,MYCBMR)
*        MODCB ACB=MYACB,MACRF=(NDF,DFR,NIS,SIS,NRM,AIX,NSR,LSR,GSR),  X
               MF=(G,MYCBMR)
*
         LA    R14,MYACB
         LR    R15,R14
         AH    R15,MYACB+ACBLENG-IHAACB
         SNAP  STORAGE=((R14),(R15)),PDATA= SNAP 10
* TEST  6: MODIFY THE ACB MYACB INLINE
* VERIFIED
         LA    R2,MYACB
         MODCB ACB=(R2),                                               X
               BSTRNO=10,                                              X
               BUFND=10,                                               X
               BUFNI=10,                                               X
               BUFSP=2000,                                             X
               DDNAME=MYACB,                                           X
               EXLST=MYAREA,                                           X
               MACRF=(IN,GSR),                                         X
               PASSWD=MYAREA,                                          X
               RMODE31=NONE,                                           X
               SHRPOOL=1,                                              X
               STRNO=10
         LTR   R15,R15            OK ?
         BNZ   ABORT              EXIT IF NOT
         LA    R14,MYACB
         LR    R15,R14
         AH    R15,MYACB+ACBLENG-IHAACB
         SNAP  STORAGE=((R14),(R15)),PDATA= SNAP 11
* TEST  7: BUILD CBMR AND MOVE IT TO MYCBMR
* VERIFIED
         MODCB ACB=MYACB,                                              X
               BSTRNO=20,                                              X
               BUFND=20,                                               X
               BUFNI=25,                                               X
               BUFSP=3000,                                             X
               DDNAME=NEWACB,                                          X
               EXLST=MYPASS,                                           X
               MACRF=(KEY,ADR,SEQ,SKP,DIR,IN,OUT,AIX,LSR,DFR,SIS),     X        X
               PASSWD=MYPASS,                                          X
               RMODE31=CB,                                             X
               SHRPOOL=2,                                              X
               STRNO=15,                                               X
               MF=(L,MYCBMR,MYLEN7)
         LTR   R15,R15            OK ?
         BNZ   ABORT              EXIT IF NOT
         SNAP  STORAGE=(MYCBMR,MYCBMR+MYLEN7),PDATA= SNAP 12
* TEST  8: MODIFY MYCBMR AND USE IT TO MODIFY MYACB
*          NOTE THAT THIS MACRF COMPLETELY REPLACES THE ONE ABOVE
* VERIFIED
         MODCB ACB=MYACB,                                              X
               DDNAME=NEWACB2,                                         X
               MACRF=(NRM,NIS,IN),                                     X
               STRNO=27,                                               X
               MF=(E,MYCBMR)
         LTR   R15,R15            OK ?
         BNZ   ABORT              EXIT IF NOT
         SNAP  STORAGE=(MYCBMR,MYCBMR+MYLEN7),PDATA= SNAP 13
         LA    R14,MYACB
         LR    R15,R14
         AH    R15,MYACB+ACBLENG-IHAACB
         SNAP  STORAGE=((R14),(R15)),PDATA= SNAP 14
* TEST  9: BUILD CBMR AND MOVE IT TO MYCBMR
*          USE CBMR TO MODIFY MYACB
*          ANY MODS ALREADY MADE TO MYACB WILL REMAIN
* VERIFIED
         MODCB ACB=MYACB,                                              X
               DDNAME=NEWACB3,                                         X
               MACRF=(AIX,SIS,IN),                                     X
               SHRPOOL=3,                                              X
               MF=(G,MYCBMR,MYLEN9)
         LTR   R15,R15            OK ?
         BNZ   ABORT              EXIT IF NOT
         SNAP  STORAGE=(MYCBMR,MYCBMR+MYLEN9),PDATA= SNAP 15
         LA    R14,MYACB
         LR    R15,R14
         AH    R15,MYACB+ACBLENG-IHAACB
         SNAP  STORAGE=((R14),(R15)),PDATA= SNAP 16
* TEST 10: BUILD CBMR AND MOVE IT TO MYCBMR
*          USE CBMR TO MODIFY MYACB
*          USES REGISTER NOTATION
*          ANY MODS ALREADY MADE TO MYACB WILL REMAIN
* VERIFIED
         LA    R2,MYACB
         LA    R3,MYPASS
         LA    R4,MYCBMR
         MODCB ACB=(R2),                                               X
               BSTRNO=20,                                              X
               BUFND=20,                                               X
               BUFNI=25,                                               X
               BUFSP=3000,                                             X
               DDNAME=NEWACB,                                          X
               EXLST=(R3),                                             X
               MACRF=(KEY,ADR,SEQ,SKP,DIR,IN,OUT,AIX,LSR,DFR,SIS),     X        X
               PASSWD=(R3),                                            X
               RMODE31=CB,                                             X
               SHRPOOL=2,                                              X
               STRNO=15,                                               X
               MF=(G,(R4),MYLEN10)
         LTR   R15,R15            OK ?
         BNZ   ABORT              EXIT IF NOT
         SNAP  STORAGE=(MYCBMR,MYCBMR+MYLEN10),PDATA= SNAP 17
         LA    R14,MYACB
         LR    R15,R14
         AH    R15,MYACB+ACBLENG-IHAACB
         SNAP  STORAGE=((R14),(R15)),PDATA= SNAP 18
***************
* EXLST TESTS *
***************
* ERROR TESTS
* EXLST BAD REG, BAD EXLST PARMS, UNSUPPORTED PARMS, REG & MF=L,
* BAD MOD, MOD L, MOD A & N, MF=L BAD REG
*        MODCB EXLST=(R15),DDNAME=ABC,BUFSP=5,KEYLEN=5,JRNAD=ABC,      X
               UPAD=ABC,RLSWAIT=ABC,AREA=(R2),EODAD=(MYPASS,Q),        X
               LERAD=(MYPASS,L),SYNAD=(MYPASS,A,N),                    X
               MF=(L,(R14),MYLEN11)
* BAD REG FOR SYNAD, NO 2ND PARM FOR MF=G, NO LABEL
*        MODCB EXLST=MYEXLST,SYNAD=((R0),N),MF=G
* BAD REG FOR MF=E
*        MODCB EXLST=MYEXLST,MF=(E,(R1))
         LA    R14,MYEXLST
         LR    R15,R14
         AHI   R15,EXLSTEND-IFGEXLST
* EXLST EODAD=MYEOD   LERAD=(0,A)  SYNAD=(MYSYN,N)
         SNAP  STORAGE=((R14),(R15)),PDATA= SNAP 19
* TEST 11: MODIFY THE EXLST MYEXLST INLINE MF=I
* VERIFIED
         LA    R2,MYEXLST
         MODCB EXLST=(R2),                                             X
               EODAD=MYPASS,                                           X
               LERAD=(0,A),                                            X
               SYNAD=(MYSYN,A)
         LTR   R15,R15            OK ?
         BNZ   ABORT              EXIT IF NOT
         LA    R14,MYEXLST
         LR    R15,R14
         AHI   R15,EXLSTEND-IFGEXLST
* EXLST EODAD=MYPASS  LERAD=(0,A)  SYNAD=(MYSYN,A)
         SNAP  STORAGE=((R14),(R15)),PDATA= SNAP 20
* TEST 12: BUILD CBMR AND MOVE IT TO MYCBMR
* VERIFIED
         MODCB EXLST=MYEXLST,                                          X
               EODAD=(MYAREA,N),                                       X
               LERAD=(4,A),                                            X
               SYNAD=(4,N),                                            X
               MF=(L,MYCBMR,MYLEN12)
         LTR   R15,R15            OK ?
         BNZ   ABORT              EXIT IF NOT
* CBMR  EODAD=(MYAREA,N)  LERAD=(4,A)  SYNAD=(4,N)
         SNAP  STORAGE=(MYCBMR,MYCBMR+MYLEN12),PDATA= SNAP 21
* TEST 13: MODIFY MYCBMR AND USE IT TO MODIFY MYEXLST
* VERIFIED
         MODCB EXLST=MYEXLST,                                          X
               EODAD=(MYEOD,N),                                        X
               LERAD=(MYLER,N),                                        X
               SYNAD=MYSYN,                                            X
               MF=(E,MYCBMR)
         LTR   R15,R15            OK ?
         BNZ   ABORT              EXIT IF NOT
* CBMR  EODAD=(MYEOD,N)   LERAD=(MYLER,N)  SYNAD=MYSYN
         SNAP  STORAGE=(MYCBMR,MYCBMR+MYLEN12),PDATA= SNAP 22
         LA    R14,MYEXLST
         LR    R15,R14
         AHI   R15,EXLSTEND-IFGEXLST
* EXLST EODAD=(MYEOD,N)   LERAD=(MYLER,N)  SYNAD=(MYSYN,N)
         SNAP  STORAGE=((R14),(R15)),PDATA= SNAP 23
* TEST 14: BUILD CBMR AND MOVE IT TO MYCBMR
*          USE CBMR TO MODIFY MYEXLST
*          ANY MODS ALREADY MADE TO MYEXLST WILL REMAIN
* VERIFIED
         MODCB EXLST=MYEXLST,                                          X
               SYNAD=(,A),                                             X
               MF=(G,MYCBMR,MYLEN14)
         LTR   R15,R15            OK ?
         BNZ   ABORT              EXIT IF NOT
* CBMR                                     SYNAD=(,A)
         SNAP  STORAGE=(MYCBMR,MYCBMR+MYLEN14),PDATA= SNAP 24
         LA    R14,MYEXLST
         LR    R15,R14
         AHI   R15,EXLSTEND-IFGEXLST
* EXLST EODAD=(MYEOD,N)   LERAD=(MYLER,N)  SYNAD=(MYSYN,A)
         SNAP  STORAGE=((R14),(R15)),PDATA= SNAP 25
* TEST 15: BUILD CBMR AND MOVE IT TO MYCBMR
*          USE CBMR TO MODIFY MYEXLST
*          USES REGISTER NOTATION
*          ANY MODS ALREADY MADE TO MYEXLST WILL REMAIN
* VERIFIED
         LA    R2,MYEXLST
         LA    R3,MYEOD
         LA    R4,MYLER
         LA    R5,MYSYN
         LA    R6,MYCBMR
         MODCB EXLST=(R2),                                             X
               EODAD=((R3),A),                                         X
               LERAD=((R4),A),                                         X
               SYNAD=((R5)),                                           X
               MF=(G,(R6),MYLEN15)
         LTR   R15,R15            OK ?
         BNZ   ABORT              EXIT IF NOT
* CBMR  EODAD=(MYEOD,A)   LERAD=(MYLER,A)  SYNAD=(MYSYN,A)
         SNAP  STORAGE=(MYCBMR,MYCBMR+MYLEN15),PDATA= SNAP 26
         LA    R14,MYEXLST
         LR    R15,R14
         AHI   R15,EXLSTEND-IFGEXLST
* EXLST EODAD=(MYEOD,A)   LERAD=(MYLER,A)  SYNAD=(MYSYN,A)
         SNAP  STORAGE=((R14),(R15)),PDATA= SNAP 27
.BYPALL  ANOP
*
         SUBEXIT
*
ABORT    DC    H'0',C'R15=8'
*
MYACB    ACB   AM=VSAM,                                                X
               DDNAME=MYACB2,                                          X
               PASSWD=MYPASS,                                          X
               EXLST=MYEXLST,                                          X
               MACRF=(KEY,ADR,SEQ,SKP,DIR,IN,OUT,AIX,LSR,DFR,SIS),     X
               BUFSP=1000,                                             X
               BUFND=5,                                                X
               BUFNI=5,                                                X
               RMODE31=CB,                                             X
               STRNO=5,                                                X
               BSTRNO=5,                                               X
               SHRPOOL=5
*
MYACB2   ACB   AM=VSAM,                                                X
               DDNAME=MYACB
*
MYRPL    RPL   ACB=MYACB,OPTCD=(ARD,KGE,GEN)
*
MYRPL2   RPL   ACB=MYACB,OPTCD=(ARD,KGE,GEN)
*
MYEXLST  EXLST AM=VSAM,                                                X
               EODAD=MYEOD,                                            X
               LERAD=(0,A),                                            X
               SYNAD=(MYSYN,N)
*
MYPASS   DC    XL50'00'
MYAREA   DC    XL100'00'
MYEOD    BR    R14
MYLER    BR    R14
MYSYN    BR    R14
MYCBMR   DC    XL100'00'
*
         ORG   *+X'B00'           PRETEND THAT OLD 19C COMES NEXT
         YREGS
         END
