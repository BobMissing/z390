         MACRO
.*###CAN RIP OUT .CODE AND &THEACB
.*###THEN COMP WITH DON'S
.**********************************************************************
.* Copyright 2005 Automated Software Tools Corporation                *
.* This source code is part of z390 assembler/emulator package        *
.* The z390 package is distributed under GNU general public license   *
.* Author - Don Higgins                                               *
.* Date   - 09/16/07                                                  *
.**********************************************************************
.* 09/16/07 RPI 682 INITIAL CODING TO GENERATE ACBMACRF FROM MACRF=   *
.* 10/15/18 Updated for zVSAM V2 by Melvyn Maltz                      *
.*          Added &MODE=                                              *
.*                CODE  generates code to modify the MACRF bits       *
.*                ACB   generates &ZGENMACF1-4 with default bits set  *
.*                      in the form stored in the ACB (14 options)    *
.*                      This includes the special &RMODEBF            *
.*                MODCB generates &ZGENMACF1-4 with only bits set     *
.*                      that are in &MACRF (17 options)               *
.*          Added &OPTS code, copied from ZGENOPTD and modified       *
.**********************************************************************
         ZGENMACF &MACRF=,&MODE=
.*
.* SET MACRF OPTION BITS
         LCLB  &KEY,&ADR,&CNV,&SEQ,&SKP,&DIR,&IN,&OUT,&NUB,&UBF
         LCLB  &NFX,&CFX,&RST,&NRS,&DDN,&DSN,&NRM,&AIX,&NSR,&LSR,&GSR
         LCLB  &RLS,&NCI,&ICI,&NDF,&DFR,&NIS,&SIS,&LEW,&NLW
.*
         GBLB  &RMODEBF           PASSED FROM ACB/MODCB/GENCB
         GBLC  &THEACB            PASSED FROM MODCB
         GBLC  &ZGENMACF1,&ZGENMACF2,&ZGENMACF3,&ZGENMACF4
.*
         AIF   (T'&MODE EQ 'O')
         MNOTE 12,'MODE= NOT SPECIFIED'
         MEXIT
         AEND
         AIF   ('&MODE' EQ 'CODE' OR '&MODE' EQ 'ACB' OR '&MODE' EQ 'MO-
               DCB').MODEOK
         MNOTE 12,'MODE= HAS INCORRECT VALUE'
         MEXIT
.*
.MODEOK  ANOP
.*
.* DEFAULT IS THE LAST ONE, SUPPORTED SETS ONLY
.* ADR     KEY        NOT MUTUALLY EXCLUSIVE
.* DFR   | NDF
.* DIR     SKP   SEQ  NOT MUTUALLY EXCLUSIVE
.* OUT     IN         NOT MUTUALLY EXCLUSIVE, OUT IMPLIES IN
.*                    OUT DOES NOT TURN ON IN
.* SIS   | NIS
.* AIX   | NRM
.* LSR   | GSR | NSR
.*
&MCFS(1) SETC  'ADR',     1                                            X
               'CNV',     2  NOT SUPPORTED                             X
               'KEY',     3                                            X
               'CFX',     4  NOT SUPPORTED                             X
               'NFX',     5  NOT SUPPORTED                             X
               'DDN',     6  NOT SUPPORTED                             X
               'DSN',     7  NOT SUPPORTED                             X
               'DFR',     8                                            X
               'NDF',     9                                            X
               'DIR',    10                                            X
               'SEQ',    11                                            X
               'SKP',    12                                            X
               'ICI',    13                                            X
               'NCI',    14                                            X
               'IN',     15                                            X
               'OUT',    16                                            X
               'LEW',    17  NOT SUPPORTED                             X
               'NLW',    18  NOT SUPPORTED                             X
               'NIS',    19                                            X
               'SIS',    20                                            X
               'NRM',    21                                            X
               'AIX',    22                                            X
               'NRS',    23  NOT SUPPORTED                             X
               'RST',    24  NOT SUPPORTED                             X
               'NSR',    25                                            X
               'LSR',    26                                            X
               'GSR',    27                                            X
               'RLS',    28  NOT SUPPORTED                             X
               'NUB',    29  NOT SUPPORTED                             X
               'UBF'     30  NOT SUPPORTED
.*
&I       SETA  0
.MCFLOOP ANOP
&I       SETA  &I+1
         AIF   (&I GT N'&MACRF).CHK_MCF  RPI 680
&J       SETA  0
.FINDMCF ANOP
&J       SETA  &J+1
         AIF   (&J GT N'&MCFS).MCF_NOT_FND
         AIF   ('&MACRF(&I)' EQ '&MCFS(&J)').MCF_FND
         AGO   .FINDMCF
.*
.MCF_NOT_FND ANOP
         MNOTE 12,'ZGENMACF MACRF PARM NOT FOUND - &MACRF(&I)'
         AGO   .MCFLOOP
.*
.MCF_FND ANOP
&KEY     SETB  ('&MACRF(&I)' EQ 'KEY' OR &KEY)
&ADR     SETB  ('&MACRF(&I)' EQ 'ADR' OR &ADR)
&CNV     SETB  ('&MACRF(&I)' EQ 'CNV' OR &CNV)
&SEQ     SETB  ('&MACRF(&I)' EQ 'SEQ' OR &SEQ)
&SKP     SETB  ('&MACRF(&I)' EQ 'SKP' OR &SKP)
&DIR     SETB  ('&MACRF(&I)' EQ 'DIR' OR &DIR)
&OUT     SETB  ('&MACRF(&I)' EQ 'OUT' OR &OUT)
&IN      SETB  ('&MACRF(&I)' EQ 'IN'  OR &IN)
&NUB     SETB  ('&MACRF(&I)' EQ 'NUB' OR &NUB)
&UBF     SETB  ('&MACRF(&I)' EQ 'UBF' OR &UBF)
&NFX     SETB  ('&MACRF(&I)' EQ 'NFX' OR &NFX)
&CFX     SETB  ('&MACRF(&I)' EQ 'CFX' OR &CFX)
&DDN     SETB  ('&MACRF(&I)' EQ 'DDN' OR &DDN)
&DSN     SETB  ('&MACRF(&I)' EQ 'DSN' OR &DSN)
&NRM     SETB  ('&MACRF(&I)' EQ 'NRM' OR &NRM)
&AIX     SETB  ('&MACRF(&I)' EQ 'AIX' OR &AIX)
&NSR     SETB  ('&MACRF(&I)' EQ 'NSR' OR &NSR)
&LSR     SETB  ('&MACRF(&I)' EQ 'LSR' OR &LSR)
&GSR     SETB  ('&MACRF(&I)' EQ 'GSR' OR &GSR)
&RLS     SETB  ('&MACRF(&I)' EQ 'RLS' OR &RLS)
&NCI     SETB  ('&MACRF(&I)' EQ 'NCI' OR &NCI)
&ICI     SETB  ('&MACRF(&I)' EQ 'ICI' OR &ICI)
&NDF     SETB  ('&MACRF(&I)' EQ 'NDF' OR &NDF)
&DFR     SETB  ('&MACRF(&I)' EQ 'DFR' OR &DFR)
&NIS     SETB  ('&MACRF(&I)' EQ 'NIS' OR &NIS)
&SIS     SETB  ('&MACRF(&I)' EQ 'SIS' OR &SIS)
&RST     SETB  ('&MACRF(&I)' EQ 'RST' OR &RST)
&NRS     SETB  ('&MACRF(&I)' EQ 'NRS' OR &NRS)
&LEW     SETB  ('&MACRF(&I)' EQ 'LEW' OR &LEW)
&NLW     SETB  ('&MACRF(&I)' EQ 'NLW' OR &NLW)
         AGO   .MCFLOOP
.*
.CHK_MCF ANOP
.* CHECK FOR UNSUPPORTED OPERANDS
         AIF   (&CNV)
         MNOTE 12,'ZGENMACF ACB CNV NOT SUPPORTED'
         AEND
         AIF   (&CFX)
         MNOTE 12,'ZGENMACF ACB CFX NOT SUPPORTED'
         AEND
         AIF   (&NFX)
         MNOTE 12,'ZGENMACF ACB NFX NOT SUPPORTED'
         AEND
         AIF   (&DDN)
         MNOTE 12,'ZGENMACF ACB DDN NOT SUPPORTED'
         AEND
         AIF   (&DSN)
         MNOTE 12,'ZGENMACF ACB DSN NOT SUPPORTED'
         AEND
         AIF   (&ICI)
         MNOTE 12,'ZGENMACF ACB ICI NOT SUPPORTED'
         AEND
         AIF   (&NCI)
         MNOTE 12,'ZGENMACF ACB NCI NOT SUPPORTED'
         AEND
         AIF   (&LEW)
         MNOTE 12,'ZGENMACF ACB LEW NOT SUPPORTED'
         AEND
         AIF   (&NLW)
         MNOTE 12,'ZGENMACF ACB NLW NOT SUPPORTED'
         AEND
         AIF   (&NRS)
         MNOTE 12,'ZGENMACF ACB NRS NOT SUPPORTED'
         AEND
         AIF   (&RST)
         MNOTE 12,'ZGENMACF ACB RST NOT SUPPORTED'
         AEND
         AIF   (&RLS)
         MNOTE 12,'ZGENMACF ACB RLS NOT SUPPORTED'
         AEND
         AIF   (&NUB)
         MNOTE 12,'ZGENMACF ACB NUB NOT SUPPORTED'
         AEND
         AIF   (&UBF)
         MNOTE 12,'ZGENMACF ACB UBF NOT SUPPORTED'
         AEND
.*
.* CHECK FOR MUTUALLY EXCLUSIVE OPTIONS OF SUPPORTED OPERANDS
.*
         AIF   (&NDF AND &DFR).DFRERR
         AGO   .CHK2
.*
.DFRERR  ANOP
         MNOTE 12,'ZGENMACF ACB NDF AND DFR MUTUALLY EXCLUSIVE'
.CHK2    ANOP
         AIF   (&NIS AND &SIS).SISERR
         AGO   .CHK3
.*
.SISERR  ANOP
         MNOTE 12,'ZGENMACF ACB NIS AND SIS MUTUALLY EXCLUSIVE'
.CHK3    ANOP
         AIF   (&NRM AND &AIX).AIXERR
         AGO   .CHK4
.*
.AIXERR  ANOP
         MNOTE 12,'ZGENMACF ACB NRM AND AIX MUTUALLY EXCLUSIVE'
.CHK4    ANOP
         AIF   (&NSR AND &LSR).NLGERR
         AIF   (&NSR AND &GSR).NLGERR
         AIF   (&LSR AND &GSR).NLGERR
         AGO   .CHKDUN
.*
.NLGERR  ANOP
         MNOTE 12,'ZGENMACF ACB NSR, LSR, AND GSR MUTUALLY EXCLUSIVE'
.CHKDUN  ANOP
.*
         AIF   ('&MODE' EQ 'CODE').CODE
         AIF   ('&MODE' EQ 'MODCB').MODCB
.*
.* SET DEFAULT OPTIONS, SUPPORTED OPERANDS ONLY
.*
&KEY     SETB  (NOT(&ADR) OR &KEY)
&SEQ     SETB  (NOT(&SKP OR &DIR) OR &SEQ)
&IN      SETB  (NOT(&OUT) OR &IN)
.*
.* GEN ACB FLAG BYTES
&ZGENMACF1 SETC '&KEY&ADR.0&SEQ&DIR&IN&OUT.0'
&ZGENMACF2 SETC '000&SKP.000&AIX'
&ZGENMACF3 SETC '0&LSR&GSR.0&DFR&SIS.0&RMODEBF'
&ZGENMACF4 SETC '00000000'
         MEXIT
.*
.MODCB   ANOP
.* GEN MODCB FLAG BYTES
&ZGENMACF1 SETC '&KEY&ADR&NDF&DFR&SEQ&DIR&SKP&IN'
&ZGENMACF2 SETC '&OUT&NIS&SIS&NRM&AIX&NSR&LSR&GSR'
&ZGENMACF3 SETC '00000000'
&ZGENMACF4 SETC '00000000'
         MEXIT
.*
.CODE    ANOP
         AIF   ('&THEACB'(1,1) EQ '(')
         ZOPTLR 1,&THEACB
         AELSE
         LA    R1,&THEACB         R1=ACB ADDRESS
         AEND
         USING IHAACB,R1
.* MAY OPTIMISE THIS LATER
         AIF   (&KEY)
         OI    ACBMACR1,ACBKEY    KEY=KEY ON
         AEND
         AIF   (&ADR)
         OI    ACBMACR1,ACBADR    ADR=ADR ON
         AEND
         AIF   (&NDF)
         NI    ACBMACR3,255-ACBDFR NDF=DFR OFF
         AEND
         AIF   (&DFR)
         OI    ACBMACR3,ACBDFR    DFR=DFR ON
         AEND
         AIF   (&SEQ)
         OI    ACBMACR1,ACBSEQ    SEQ=SEQ ON
         AEND
         AIF   (&DIR)
         OI    ACBMACR1,ACBDIR    DIR=DIR ON
         AEND
         AIF   (&SKP)
         OI    ACBMACR2,ACBSKP    SKP=SKP ON
         AEND
         AIF   (&IN)
         OI    ACBMACR1,ACBIN     IN=IN ON
         AEND
.* OUT ALLOWS IN
         AIF   (&OUT)
         OI    ACBMACR1,ACBOUT     OUT=OUT ON
         AEND
         AIF   (&NIS)
         NI    ACBMACR3,255-ACBSIS NIS=SIS OFF
         AEND
         AIF   (&SIS)
         OI    ACBMACR3,ACBSIS    SIS=SIS ON
         AEND
         AIF   (&NRM)
         NI    ACBMACR2,255-ACBAIX NRM=AIX OFF
         AEND
         AIF   (&AIX)
         OI    ACBMACR2,ACBAIX    AIX=AIX ON
         AEND
         AIF   (&NSR)
         NI    ACBMACR3,255-(ACBLSR+ACBGSR) NSR=LSR+GSR OFF
         AEND
         AIF   (&LSR)
         OI    ACBMACR3,ACBLSR    LSR=LSR ON
         NI    ACBMACR3,255-ACBGSR LSR=GSR OFF
         AEND
         AIF   (&GSR)
         OI    ACBMACR3,ACBGSR    GSR=GSR ON
         NI    ACBMACR3,255-ACBLSR GSR=LSR OFF
         AEND
         AIF   (&RMODEBF)
         OI    ACBMACR3,ACBMODE   RMODE31=ALL/BUFF MODE=ON
         AEND
         DROP  R1
         MEND
